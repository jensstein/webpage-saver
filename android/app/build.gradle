plugins {
    id 'com.android.application'
    id 'kotlin-android'
}
apply plugin: 'kotlin-android'

// https://stackoverflow.com/a/69950532
def OPENID_CLIENT_ID_TAG = "openid_client_id"
def OPENID_PROPERTIES_FILENAME = "openid.properties"
def properties = new Properties()
try {
    properties.load(new FileInputStream(rootProject.file(OPENID_PROPERTIES_FILENAME)))
    if (!properties.containsKey(OPENID_CLIENT_ID_TAG)) {
        throw new InvalidUserDataException("Missing $OPENID_CLIENT_ID_TAG property in gradle.properties")
    }
} catch (FileNotFoundException e) {
    throw new FileNotFoundException("Expected a file $OPENID_PROPERTIES_FILENAME with openid configuration ($e)")
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "dk.jens.webpage_saver"
        minSdk 31
        targetSdk 33
        versionCode 1
        versionName "1.0"

        // https://github.com/openid/AppAuth-Android/issues/128#issuecomment-477551721
        manifestPlaceholders = [
            appAuthRedirectScheme: "dk.jens.webpagesaver"
        ]

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField("String", "OPENID_CLIENT_ID", properties[OPENID_CLIENT_ID_TAG])
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.2'
    implementation 'net.openid:appauth:0.11.1'
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-jackson:2.9.0"
    testImplementation 'junit:junit:4.13.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
